apply plugin:rootProject.ext.plugins.application

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion

    defaultConfig {
        applicationId "com.hsj.chat"
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        resourcePrefix "chat_"

        // 矢量图
        vectorDrawables.useSupportLibrary = true

        //打包时筛选出这两种架构
        ndk { abiFilters "armeabi-v7a", "x86" }

        testInstrumentationRunner rootProject.ext.dependencies.testInstrumentationRunner
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            debuggable false
            jniDebuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "App_${variant.productFlavors[0].name}_v${variant.versionName}_v${variant.versionCode}.apk"
        }
    }

    flavorDimensions "default"
    productFlavors {
        chat_plugin { dimension "default" }
    }

    dataBinding {
        enabled = true
    }

    packagingOptions {
        exclude 'AndroidManifest.xml'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation rootProject.ext.dependencies.testJunit
    androidTestImplementation rootProject.ext.dependencies.testRunner
    androidTestImplementation rootProject.ext.dependencies.testEspresso

    implementation project(':Library:BaseLib')
    implementation rootProject.ext.dependencies.repluginLib
}

apply plugin: rootProject.ext.plugins.repluginLib

repluginPluginConfig {
    //插件名
    pluginName = "chat"
    //宿主app的包名
    hostApplicationId = "com.hsj"
    //宿主app的启动activity
    hostAppLauncherActivity = "com.hsj.LaunchActivity"
}


